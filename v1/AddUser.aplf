 resp←req AddUser ns;user;rc;msg;users
 ⍝ ns.(login password)
 ⍝ resp.(rc msg)
 ⍝   rc - 0 success
 ⍝        1 login already exists
 ⍝        2 database failure
 resp←utils.initializeResponse

 :Hold 'database'

     :If 0≠⊃(rc msg users)←dbapi.readUsers
         →0⊣resp.(rc msg)←rc msg
     :EndIf

     :If 0≠users.login utils.indexOf ⊆,ns.login
         →0⊣resp.(rc msg)←1('user ',ns.login,' already exists')
     :EndIf

     ns.password←utils.hashPassword ns.password
     ns.updatedAt←utils.now
     users,←ns
     →0⍴⍨0≠(resp←dbapi.writeUsers users).rc

     resp.(rc msg)←0('user ',ns.login,' added')

 :EndHold
