 resp←req UpdatePhonebookEntry ns;rc;msg;phonebook;index
 ⍝ ns.(ext [fname] [lname] [password])
 ⍝ resp.(rc msg)
 ⍝   rc - 0 success
 ⍝        1 some error in request
 ⍝        2 database failure

 resp←utils.initializeResponse

 :Hold 'database'

     :If 0≠⊃(rc msg phonebook)←dbapi.readPhonebook
         →0⊣resp.(rc msg)←rc msg
     :EndIf

     :If 0=index←phonebook.ext utils.indexOf ns.ext
         →0⊣resp.(rc msg)←1('extension ',ns.ext,' does not exist')
     :EndIf

     ns.updatedAt←utils.now
     →0⍴⍨0≠(resp←phonebook[index]dbapi.mergePhonebookEntry ns).rc
     phonebook[index]←resp.payload
     →0⍴⍨0≠(resp←dbapi.writePhonebook phonebook).rc

     resp.msg←'extension ',ns.ext,' updated'

 :EndHold
